buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    signingConfigs {
        cybertron {
            keyAlias rootProject.ext.keystore['keyAlias']
            keyPassword rootProject.ext.keystore['keyPassword']
            storeFile file(rootProject.ext.keystore['storeFile'])
            storePassword rootProject.ext.keystore['storePassword']
        }
    }

    defaultConfig {
        applicationId "com.cybertron.launcher3"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
    }
    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            versionNameSuffix "_release"
            signingConfig signingConfigs.cybertron
        }

        debug {
            minifyEnabled false
        }
    }
    sourceSets {
        main {
            res.srcDirs = ['res', 'WallpaperPicker/res']
            main.java.srcDirs = ['src', 'WallpaperPicker/src']
            manifest.srcFile 'AndroidManifest.xml'
            proto.srcDirs 'protos/'
        }
    }

    lintOptions {
        ignoreWarnings true
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

repositories {
    mavenCentral()
}

build.doLast {
    copy {
        from("$buildDir/outputs/apk") {
            include '**/*release.apk'
        }
        into(releaseOutputs)
    }
}


dependencies {
    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.android.support:recyclerview-v7:25.0.0'
    compile 'com.google.protobuf:protobuf-lite:3.0.0'
    compile 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-2'
    compile 'com.google.protobuf:protoc:3.0.0-alpha-3'
}



protobuf {
    //这里配置protoc编译器
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'
    }

    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }

    //这里配置生成目录，编译后会在build的目录下生成对应的java文件
    generateProtoTasks {
        all().each { task ->

            task.builtins {
                remove java
                javanano {
                    option 'ignore_services=false'
                }
            }

        }
    }
}
